
###########################################################################
# Cobblestone replacing
###########################################################################
Tweaker.setPlacementStage("PRE_POPULATE")

# allBiomes = forAllBiomes()
# allBiomes.set("contiguousReplacement", true)

#
toLimestone_chaparral = newBlockReplacement()
toLimestone_chaparral.set("block", "divergentunderground:quark_limestone_cobblestone")
toLimestone_chaparral.set("maxY", 88)
toLimestone_chaparral.set("minY", 60)

toMarble_chaparral = newBlockReplacement()
toMarble_chaparral.set("block", "divergentunderground:quark_marble_cobblestone")
toMarble_chaparral.set("maxY", 200)
toMarble_chaparral.set("minY", 89)

chaparral = forBiomes("biomesoplenty:chaparral")
chaparral.registerGenBlockRep("minecraft:cobblestone", toLimestone_chaparral)
chaparral.registerGenBlockRep("minecraft:cobblestone", toMarble_chaparral)
#

#
toMarble_Crag = newBlockReplacement()
toMarble_Crag.set("block", "divergentunderground:quark_marble_cobblestone")
toMarble_Crag.set("maxY", 103)
toMarble_Crag.set("minY", 40)

toGranite_Crag = newBlockReplacement()
toGranite_Crag.set("block", "divergentunderground:granite_cobblestone")
toGranite_Crag.set("maxY", 132)
toGranite_Crag.set("minY", 102)

toAndesite_Crag = newBlockReplacement()
toAndesite_Crag.set("block", "divergentunderground:andesite_cobblestone")
toAndesite_Crag.set("maxY", 255)
toAndesite_Crag.set("minY", 132)

bopCrag = forBiomes("biomesoplenty:crag")
bopCrag.registerGenBlockRep("minecraft:cobblestone", toMarble_Crag)
bopCrag.registerGenBlockRep("minecraft:cobblestone", toGranite_Crag)
bopCrag.registerGenBlockRep("minecraft:cobblestone", toAndesite_Crag)
#

#
toMarble_ExtremeHills = newBlockReplacement()
toMarble_ExtremeHills.set("block", "divergentunderground:quark_marble_cobblestone")
toMarble_ExtremeHills.set("maxY", 103)
toMarble_ExtremeHills.set("minY", 40)

toGranite_ExtremeHills = newBlockReplacement()
toGranite_ExtremeHills.set("block", "divergentunderground:granite_cobblestone")
toGranite_ExtremeHills.set("maxY", 132)
toGranite_ExtremeHills.set("minY", 102)

toAndesite_ExtremeHills = newBlockReplacement()
toAndesite_ExtremeHills.set("block", "divergentunderground:andesite_cobblestone")
toAndesite_ExtremeHills.set("maxY", 255)
toAndesite_ExtremeHills.set("minY", 131)

ExtremeHills = forBiomes("minecraft:extreme_hills", "minecraft:extreme_hills_with_trees", "minecraft:mutated_extreme_hills", "minecraft:mutated_extreme_hills_with_trees")
ExtremeHills.registerGenBlockRep("minecraft:cobblestone", toMarble_ExtremeHills)
ExtremeHills.registerGenBlockRep("minecraft:cobblestone", toGranite_ExtremeHills)
ExtremeHills.registerGenBlockRep("minecraft:cobblestone", toAndesite_ExtremeHills)

pathBlock = forBlock("dawnoftimebuilder:path_dirt")

pathSpots = newSplotchDecoration()
pathSpots.addBlockToReplace("minecraft:grass")
pathSpots.addBaseBlock("minecraft:dirt")
pathSpots.set("count", 50)
pathSpots.set("mainBlock", pathBlock)
pathSpots.set("size", 10)

ExtremeHills.addDecoration(pathSpots)
#

#
toDiorite_ExtremeHillsEdge = newBlockReplacement()
toDiorite_ExtremeHillsEdge.set("block", "divergentunderground:diorite_cobblestone")
toDiorite_ExtremeHillsEdge.set("maxY", 255)
toDiorite_ExtremeHillsEdge.set("minY", 64)

ExtremeHillsEdge = forBiomes("minecraft:smaller_extreme_hills")
ExtremeHillsEdge.registerGenBlockRep("minecraft:cobblestone", toDiorite_ExtremeHillsEdge)
#

#
toGranite_Taiga = newBlockReplacement()
toGranite_Taiga.set("block", "divergentunderground:granite_cobblestone")
toGranite_Taiga.set("maxY", 200)
toGranite_Taiga.set("minY", 62)

Taiga = forBiomes("minecraft:taiga", "minecraft:taiga_hills", "minecraft:taiga_cold", "minecraft:taiga_cold_hills", "minecraft:redwood_taiga", "minecraft:redwood_taiga_hills", "minecraft:mutated_taiga", "minecraft:mutated_taiga_cold", "minecraft:mutated_redwood_taiga", "minecraft:mutated_redwood_taiga_hills")
Taiga.registerGenBlockRep("minecraft:cobblestone", toGranite_Taiga)
#

#
toDiorite_Jungle = newBlockReplacement()
toDiorite_Jungle.set("block", "divergentunderground:diorite_cobblestone")
toDiorite_Jungle.set("maxY", 151)
toDiorite_Jungle.set("minY", 64)

Jungle = forBiomes("minecraft:jungle", "minecraft:jungle_hills", "minecraft:jungle_edge", "minecraft:mutated_jungle", "minecraft:mutated_jungle_edge")
Jungle.registerGenBlockRep("minecraft:cobblestone", toDiorite_Jungle)
#